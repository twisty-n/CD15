--NPROG <program>::= program <id> <arrays> <procs> <main> end program <id>
    Special <arrays> ::= arrays <arraydecl> <arrdltail>
    Special <arrays> ::= ?
--NARRYL <arrdltail> ::= , <arraydecl> <arrdltail>
    Special <arrdltail> ::= ?
--NPROCL <procs> ::= <proc> <procs>
    Special <procs> ::= ?
--NMAIN <main> ::= local <idlist> ; <stats>
--NARRDEC <arraydecl> ::= <id> [ <intlit> ]
--NPROC <proc> ::= proc <id> <parameters> <locals> <stats> end proc <id>
--NPARAMS <parameters> ::= var <plist> <paramstail>
--NPARAMS <parameters> ::= val <pidlist>
    Special <parameters> ::= ?
    Special <paramstail> ::= val <pidlist>
    Special <paramstail> ::= ?
        Special <plist> ::= <param> <plisttail>
--NPLIST <plisttail> ::= , <plist>
        Special <plisttail> ::= ?
    Special <param> ::= <id> <paramtail>
--NSIMPAR <paramtail> ::= ?
--NARRPAR <paramtail> ::= [ ]
--NSIMPAR <pidlist> ::= <id> <pidltail>
--NPLIST <pidltail> ::= , <pidlist>
    Special <pidltail> ::= ?
    Special <locals> ::= local <decllist> ; | ?
    Special <decllist> ::= <decl> <dltail>
--NDLIST <dltail> ::= , <decllist>
    Special <dltail> ::= ?
    Special <decl> ::= <id> <dtail>
--NARRDEC <dtail> ::= [ <intlit> ]
--NSIMDEC <dtail> ::= ?
    Special <stats> ::= <stat> <sltail>
--NSLIST <sltail> ::= <stat> <sltail>
    Special <sltail> ::= ?
    Special <stat> ::= <loopstat> | <exitstat> | <ifstat>
    Special <stat> ::= <asgnstat> | <iostat> | <callstat>
--NLOOP <loopstat> ::= loop <id> <stats> end loop <id>
--NEXIT <exitstat> ::= exit <id> when <bool> ;
    Special <ifstat> ::= if <bool> then <stats> <iftail>
--NIFT <iftail> ::= end if
--NIFTE <iftail> ::= else <stats> end if
--NIFTE <iftail> ::= elsif <bool> then <stats> <elsiftail>
--NIFTE <elsiftail> ::= elsif <bool> then <stats> <elsiftail>
    Special <elsiftail> ::= else <stats> end if
    Special <asgnstat> ::= <var> <asgnop> <expr> ;
--NASGN, NPLEQ, NMNEQ, NSTEQ, NDVEQ
<asgnop> ::= = | += | -= | *= | /=
--NINPUT <iostat> ::= input <vlist> ;
--NPRINT <iostat> ::= print <prlist> ;
--NPRLN <iostat> ::= printline <printail>
    Special <printail> ::= <prlist> ;
    Special <printail> ::= ;
--NCALL <callstat> ::= call <id> <calltail>
    Special <calltail> ::= with <elist> ;
    Special <calltail> ::= ;
    Special <vlist> ::= <var> <vltail>
NVLIST <vltail> ::= , <vlist>
    Special <vltail> ::= ?
    Special <var> ::= <id> <vtail>
NARRVAR <vtail> ::= [<expr>]
NSIMVAR <vtail> ::= ?
    Special <elist> ::= <expr> <eltail>
NELIST <eltail> ::= , <elist>
    Special <eltail> ::= ?
    Special <bool> ::= <rel> <btail>
    Special <btail> ::= <logop> <rel> <btail>
    Special <btail> ::= ?
NAND, NOR, NXOR
<logop> ::= and | or | xor
NNOT <rel> ::= not <rel>
    Special <rel> ::= <expr> <relop> <expr>
NEQL, NNEQ, NGTR, NLEQ, NLESS, NGEQ
<relop> ::= == | != | > | <= | < | >=
    Special <expr> ::= <term> <etail>
NADD <etail> ::= + <term> <etail>
NSUB <etail> ::= - <term> <etail>
    Special <etail> ::= ?
    Special <term> ::= <fact> <ttail>
NMUL <ttail> ::= * <fact> <ttail>
NDIV <ttail> ::= / <fact> <ttail>
NIDIV <ttail> ::= div <fact> <ttatil>
    Special <ttail> ::= ?
    Special <fact> ::= <id> <ftail>
NILIT <fact> ::= <intlit>
NFLIT <fact> ::= <reallit>
    Special <fact> ::= ( <bool> )
    Special <ftail> ::= <vtail>
NLEN <ftail> ::= . length
    Special <prlist> ::= <printitem> <prltail>
NPRLIST <prltail> ::= , <prlist>
    Special <prltail> ::= ?
    Special <printitem> ::= <expr>
NSTRG <printitem> ::= <string>
NSIMDEC <idlist> ::= <id> <idltail>
NIDLST <idltail> ::= , <idlist>
    Special <idltail> ::= ?
